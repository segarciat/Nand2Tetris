class Player {
  field int x;
  field int y;
  field int vx;
  field int vy;
  field int width;
  field int height;
  field boolean isJumping;


  constructor Player new(int startX, int startY){
    let x = startX;
    let y = startY;
    let vx = 0;
    let vy = 0;
    let width = 10;
    let height = 20;
    let isJumping = false;
    return this;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  method int getX() {
    return x;
  }

  method int getY() {
    return y;
  }

  method int getWidth() {
    return width;
  }

  method int getHeight() {
    return height;
  }

  method void draw() {
    do Screen.setColor(true);
    do Screen.drawRectangle(x, y, x + width, y + height);
    return;
  }

  method void erase() {
    do Screen.setColor(false);
    do Screen.drawRectangle(x, y, x + width, y + height);
    return;
  }

  method void stop() {
    let vx = 0;
    return;
  }

  method void moveRight() {
    if(x < (510 - width)) {
      let vx = 2;
    }
    return;
  }

  method void moveLeft() {
    if(x > 2){
      let vx = -2;
    }
    return;
  }

  method void jump(){
    if(~isJumping){
      let vy = -20;
      let isJumping = true;
    }
    return;
  }

  method void update(){
    if((~(vx = 0)) | (~(vy = 0))){
      do erase();
      let x = x + vx;
      let y = y + vy;
      if(y > (254 - height)){
        let y = 254 - height;
        let isJumping = false;
        let vy = 0;
      }
      do draw();
    }
    let vy = vy + 2;
    return;
  }
}