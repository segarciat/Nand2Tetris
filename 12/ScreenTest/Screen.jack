// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static int screen;
    static boolean color;
    static Array twoToThe;

    /** Initializes the Screen. */
    function void init() {
        var int j;
        let screen = 16384;
        // black by default.
        let color = true;
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let j = 1;
        while(j < 16) {
            let twoToThe[j] = twoToThe[j - 1] + twoToThe[j - 1];
            let j = j + 1;
        }
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int r, c;
        let r = 0;
        do Screen.setColor(false);
        while(r < 256) {
            let c = 0;
            while(c < 512) {
                do Screen.drawPixel(r, c);
            }
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int j;
        var int val;
        // Get 16-bit word that contains the pixel of interest.
        let val = Memory.peek(screen + (y * 32) + (x / 16));

        // Find out which bit we're changing.
        let j = x - (16 * (x / 16));

        // Change the appropriate bit depending on the color.
        if(color) {
            let val = val | twoToThe[j];
        } else {
            let val = val & (~twoToThe[j]);
        }
        do Memory.poke(screen + (y * 32) + (x / 16), val);
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int a, b, dx, dy, adyMinusbdx, temp;
        var int xDir, yDir;
        let xDir = 1;
        let yDir = 1;
        let dx = x2 - x1;
        let dy = y2 - y1;
        let a = 0;
        let b = 0;

        if(dx < 0) {
            let xDir = -1;
            let dx = -dx;
        }

        if(dy < 0) {
            let yDir = -1;
            let dy = -dy;
        }

        // Horirzontal line.
        if(dy = 0) {
            while(~(a > dx)) {
                do Screen.drawPixel(x1 + (xDir*a), y1);
                let a = a + 1;
                //do Output.printInt(a);
                //do Output.printString(" ");
            }
            return;
        }

        // Vertical line.
        if(dx = 0) {
            while(~(b > dy)) {
                do Screen.drawPixel(x1, y1 + (yDir*b));
                let b = b + 1;
            }
            return;
        }
        let adyMinusbdx = 0;
        while(~(a > dx) & ~(b > dy)) {
            do Screen.drawPixel(x1 + (xDir*a), y1 + (yDir*b));
            if(adyMinusbdx < 0) {
                let a = a + 1;
                let adyMinusbdx = adyMinusbdx + dy;
            }
            else {
                let b = b + 1;
                let adyMinusbdx = adyMinusbdx - dx;
            }
        }
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int y;
        let y = y1;
        while(~(y > y2)) {
            do Screen.drawLine(x1, y, x2, y);
            let y = y + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy;
        var int dx;
        let dy = -r;
        while(~(dy > r)) {
            let dx = Math.sqrt((r * r) - (dy * dy));
            do Screen.drawLine(x - dx, y + dy, x + dx, y + dy);
            let dy = dy + 1;
        }
        return;
    }
}
